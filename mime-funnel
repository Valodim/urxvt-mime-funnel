#!/usr/bin/perl
# Author:  Vincent Breitmoser <valodim@mugenguild.com>
# Website: https://github.com/Valodim/urxvt-mime-funnel
# License: GPLv2

use strict;
use warnings;
use File::Temp qw/ tempfile tempdir /;

# usage:
# printf "\033]777;mime-funnel;image/png;\007" ; base64 image.png ; echo '!'

my $funnel_type = 0;
my $funnel_name = '';
my $funnel_timer = 0;

sub on_start {
    my ($self) = @_;

    $self->{"funnel-code"} = $self->x_resource("funnel-code") || 0;
    $self->{"funnel-timeout"} = int($self->x_resource("funnel-timeout")) || 5;
    $self->{"mime-launcher"} = $self->x_resource("mime-launcher") || "run-mailcap";

    ()
}

sub on_osc_seq_perl {
    my ($self, $osc, $resp) = @_;

    if ($funnel_type) {
        $self->scr_add_lines ( "osc sequence within funneled data?! this should never happen!" );
        return;
    }

    return unless $osc =~ s/^mime-funnel;//;

    # parse mimetype argument
    $osc =~ s/^([^;]+);//
        or return;
    # save mimetype for later
    $funnel_type = $1;

    # make sure they are even allowed to do this!
    if ($self->{"funnel-code"}) {
        $osc =~ s/^([^;]+);//;
        if($self->{"funnel-code"} ne $1) {
            # make sure funneling mode is not active!!
            $funnel_type = 0;
            $self->scr_add_lines ("# incorrect security code, funneled data rejected!\r\n");
            return 1;
        }
    }

    # get a temp file
    (my $fh, $funnel_name) = tempfile();
    $funnel_name =~ s/\n//;
    # open a pipe to base64, written to the temp file
    open(FUNNEL_FD, '| base64 -d >' . $funnel_name);
    binmode FUNNEL_FD;
    # status msg
    $self->scr_add_lines ("# receiving funneled data...\r\n");

    $funnel_timer = urxvt::timer
         ->new ->after($self->{"funnel-timeout"}) ->cb (sub {
            if($funnel_type) {
                $funnel_type = 0;
                $self->scr_add_lines ("# funneling timeout!\r\n");
                $self->want_refresh();
            }
         });

    1
}

sub on_add_lines {
    my ($self, $line) = @_;

    if ($funnel_type) {
        # base64 complains about \r, so strip it out
        $line =~ s/\r//g;
        # got bang?
        if(index($line, '!') != -1) {
            $funnel_timer->stop();
            # print rest of data until the !
            print FUNNEL_FD substr ($line, 0, index($line, '!'));
            close FUNNEL_FD;
            # run run-mailcap with appropiate mimetype
            system($self->{"mime-launcher"} . " $funnel_type:$funnel_name; rm $funnel_name");
            $funnel_timer = 0;
            $funnel_type = 0;
            return 1;
        } else {
            # just passin' through
            print FUNNEL_FD $line;
            return 1;
        }

    }

    ()
}


# vim: set et ts=4 sw=4:

